plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.openapi.generator' version '7.0.1'
}

group = 'com.coomiix'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-data-mongodb"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-api:2.2.0"
    implementation "org.springframework.boot:spring-boot-starter-amqp"
    implementation "ch.qos.logback:logback-classic"
    implementation 'me.paulschwarz:spring-dotenv:4.0.0'
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    implementation 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.4'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.1'
    implementation 'org.reflections:reflections:0.10.2'

    
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:mongodb"
    testImplementation "org.testcontainers:rabbitmq"
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    dependsOn(tasks.openApiGenerate)
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/src/main/resources/openapi.yaml"
    outputDir = "$buildDir/generated"
    apiPackage = "com.coomiix.admin.api"
    modelPackage = "com.coomiix.admin.model"
    configOptions = [
        interfaceOnly: "true",
        useTags: "true",
        dateLibrary: "java8-localdatetime",
        useSpringBoot3: "true"
    ]
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/java"
        }
    }
}
